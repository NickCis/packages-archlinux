# $Id$
# Maintainer: Nicolas Cisco <ncis20@gmail.com>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/nickcis/packages-archlinux
#       Otherwise, open a GitHub issue.
#       This package was inspired in the official Arch linux's go package
#       This package is only intended to be use for my docker-i686 package
#


pkgname=go-docker
pkgver=1.4.3
pkgrel=1
pkgdesc='Compiler and tools for the Go programming language from Google. This packages is intended to provide a suitable go version for docker in i686 systems.'
arch=('i686')
url='http://golang.org/'
license=('BSD')
makedepends=('inetutils' 'git' 'gcc' 'fakeroot' 'pkg-config' 'go')
options=('!strip' 'staticlibs')
optdepends=('mercurial: for fetching sources from mercurial repositories'
            'git: for fetching sources from git repositories'
            'bzr: for fetching sources from bazaar repositories'
            'subversion: for fetching sources from subversion repositories')
install="$pkgname.install"
source=("$pkgname-$pkgver::git+https://go.googlesource.com/go#tag=go$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname-$pkgver/src"

  export GOPATH="$srcdir/"
  export GOROOT_FINAL=/usr/lib/go-docker
  export GOROOT_BOOTSTRAP=/usr/lib/go

  export GOOS=linux
  export GOARCH=386

  bash make.bash --no-clean
}

check() {
  cd "$pkgname-$pkgver"

  export GOOS=linux
  export GOARCH=386

  export GOROOT="$srcdir/$pkgname-$pkgver"
  export GOBIN="$GOROOT/bin"
  export PATH="$srcdir/$pkgname-$pkgver/bin:$PATH"
  export GOROOT_BOOTSTRAP=/usr/lib/go
  export GOROOT_FINAL=/usr/lib/go-docker

  # TestSimpleMulticastListener will fail in standard chroot
  cd src && bash run.bash --no-rebuild || true
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/go-docker/LICENSE"

  mkdir -p \
    "$pkgdir/usr/"{share/go-docker,lib/go-docker,lib/go-docker/src,lib/go-docker/site/src}

  cp -r doc misc -t "$pkgdir/usr/share/go-docker"
  ln -s /usr/share/go-docker/doc "$pkgdir/usr/lib/go-docker/doc"

  cp -a pkg "$pkgdir/usr/lib/go-docker/"
  cp -a src "$pkgdir/usr/lib/go-docker/"
  cp -a lib "$pkgdir/usr/lib/go-docker/"
  cp -a bin "$pkgdir/usr/lib/go-docker/"

  cp -r misc/ "$pkgdir/usr/lib/go-docker/"

  install -Dm644 VERSION "$pkgdir/usr/lib/go-docker/VERSION"

  # Clean Windows specific files.
  rm -f "$pkgdir"/usr/lib/go-docker/src/*.bat
}

# vim:set ts=2 sw=2 et:
